# SPDX-FileCopyrightText: The Threadbare Authors
# SPDX-License-Identifier: MPL-2.0
~ start
do puzzle.stop_hints()
if puzzle.is_solved():
	=> item_hint

if not first_conversation:
	=> hello_again

set first_conversation = false

Musician: Ah! Another wanderer. Welcome to the Song Sanctuary. Or what's left of it.
StoryWeaver: What happened here?
Musician: Songs stopped being sung. And when a song's forgotten... the world forgets itself right along with it.
if puzzle.get_progress() >= 1:
	Musician: Oh! Have you been playing with the stones over there?
	StoryWeaver: I kicked them and something strange happened...
~ about_stones
Musician: These stones? They're part of a magical song that used to open the portal to the Ink Well, where songwriters found ink to write their songs. But...
Musician: Um... I can't recall the first few notes of The Magical Melody. Neither the rest of it...
Musician: But they say music can live in the most unexpected places. Maybe if you explore around you may be able to find the melody.
=> END

~ hello_again
Musician: Hello again, StoryWeaver. Would you like to hear about the stones again?
- Yes => about_stones
- No
	Musician: OK then, good luck.
	=> END
- Do you remember anything else?
	if puzzle.get_progress() == 0:
		=> hint_melody_0
	if puzzle.get_progress() == 1:
		=> hint_melody_1
	if puzzle.get_progress() == 2:
		=> hint_melody_2
	if puzzle.get_progress() == 3:
		=> hint_melody_3
	Musician: Good luck out there.
	=> END

~ hint_melody_0
if get_limited_hint_level() == 0:
	Musician: I can't recall the song... But someone nearby may remember it. Try exploring around.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: When you play the right notes, you'll know.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: I believe I heard the Songstress across the river hum a familiar tune. You should go talk to her.
	do advance_hint_level()
	=> END

~ hint_melody_1
if get_limited_hint_level() == 0:
	Musician: The signs hold the key. They alter the melody in different ways.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: Someone around may know more about the signs. You may even find them somewhere else.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: There's the Luthier in the mountain region past the river. I know he studied the signs for a while.
	do advance_hint_level()
	=> END

~ hint_melody_2
if get_limited_hint_level() == 0:
	Musician: This other sign is similar, and yet... it feels the opposite.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: The forest hides secrets that may help you figure this out.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: There's a glade nearby. The Minstrel lives there. He can help you.
	do advance_hint_level()
	=> END

~ hint_melody_3
if get_limited_hint_level() == 0:
	Musician: I don't recall anything more... But you are so close! I know you can do this.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: If you reached this far, you know what comes next.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: The Minstrel in the glade nearby may help you, if you haven't seen him yet.
	do advance_hint_level()
	=> END

~ well_done
Musician: The melody... it's not just music. It opens the portal to the Ink Well. Let's go find more ink so songs and stories can be written again.
Musician: But we must be careful, the InkDrinkers dwell there!
=> END

~ item_hint
Musician: (PLACEHOLDER) You did it! The melody worked. I left a special item near the stones, grab it before you go!
=> END
