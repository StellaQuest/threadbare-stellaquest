# SPDX-FileCopyrightText: The Threadbare Authors
# SPDX-License-Identifier: MPL-2.0
~ start
if puzzle.is_solved():
	=> item_hint

if not first_conversation:
	=> hello_again

set first_conversation = false

Musician: Ah! Another wanderer. Welcome to the Song Sanctuary. Or what's left of it.
StoryWeaver: What happened here?
Musician: Songs stopped being sung. And when a song's forgotten... the world forgets itself right along with it.
if puzzle.get_progress() >= 1:
	Musician: Oh! Have you been playing with the stones over there?
	StoryWeaver: I kicked them and something strange happened...
Musician: These stones? They're part of a magical song that used to open the portal to the Ink Well, where songwriters found ink to write their songs. But...
Musician: Um... Anyway, now I can only recall the first few notes of the magical melody. The rest slips away.
StoryWeaver: Can you show me what you remember? Maybe I can help.
=> demonstrate_melody

~ hello_again
Musician: Hello again, StoryWeaver. Would you like me to play the magical melody again?
- Yes => demonstrate_melody
- No
	Musician: OK then, good luck.
	=> END

~ demonstrate_melody
Musician: I'll try. Let's see...
Musician: do,[do play("C")] re,[do play("D")] mi,[do play("E")] sol,[do play("G")] ...
Musician: And then it fades.
if first_conversation:
	StoryWeaver: What if I tried to complete it?
	Musician: Yes, please!
Musician: I wish you luck.
=> END

~ well_done
Musician: The melody... it's not just music. It opens the portal to the Ink Well. Let's go find more ink so songs and stories can be written again.
Musician: But we must be careful, the InkDrinkers dwell there!
=> END

~ item_hint
Musician: (PLACEHOLDER) You did it! The melody worked. I left a special item near the stones, grab it before you go!
=> END
